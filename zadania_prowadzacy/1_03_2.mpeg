###########
# BO-L2-P03
###########
option solver cplex;
reset;
param K;             # liczba  lat
set   P;             # zbior   projektow
param koszt{P};      # koszt   projektu
param przychod{P};   # roczny  przychod
param proc{P};       # procent ukonczenia
param budzet{1..K}, integer;  # budzet w latch
param s{P, 1..K};    # struktura projekt/rok
var x{P, 1..K} >= 0; # procent i-tego projektu ukonczonego w j-tym roku
maximize zysk: sum{i in P, j in 1..K} (K-j) * przychod[i] * x[i,j];
subject to
c1{i in P}:            proc[i] <= sum{j in 1..K} x[i,j]<=1;
c2{j in 1..K}:         sum{i in P} koszt[i]*x[i,j] <= budzet[j];
c3{i in P, j in 1..K}: x[i,j] <= s[i,j];
data;
param  K:=5;
param: P: koszt,  przychod,  proc  := 
  "P1",     5,     0.05,      1
  "P2",     8,     0.07,      0.25
  "P3",    15,     0.15,      0.25
  "P4",     1.2,   0.02,      1;
param budzet := 
  1  3
  2  6
  3  7
  4  7
  5  7;
param s: 1   2   3   4   5   :=
  "P1"   1   1   1   0   0 
  "P2"   0   1   1   1   1
  "P3"   1   1   1   1   1 
  "P4"   0   0   1   1   0;
expand  zysk; 
solve;
display zysk, x;
#############################################################################################
# zysk = 0.52375
# x :=
# P1 1   0
# P1 2   1
# P1 3   0
# P1 4   0
# P1 5   0
# P2 1   0
# P2 2   0
# P2 3   0
# P2 4   0.225
# P2 5   0.025
# P3 1   0.2
# P3 2   0.0666667
# P3 3   0.386667
# P3 4   0.346667
# P3 5   0
# P4 1   0
# P4 2   0
# P4 3   1
# P4 4   0
# P4 5   0
# ;
#############################################################################################