###########
# BO-L2-P04
###########
option solve cplex;
reset;
param  k;                   # okres inwestowania
param m0;                   # zapas poczatkowy
param g0;                   # gotowka poczatkowa
param s0;                   # sprzedaz poczatkowa (zakladamy sprzedaz 0 przed styczniem)
param pojemnosc;            # pojemnosc magazynu
param cena_k{1..k};         # cena zakupu 1 tony na poczatku miesiecy 1..4
param cena_s{1..k};         # cena sprzedazy 1 tony na koncu miesiecy 1..4
param  popyt{1..k};         # popyt na kukurydze w miesiacach 1..4
var z{1..k} >= 0;   # wielkosci zakupow   na poczatku miesiaca 1..k
var m{0..k} >= 0;   # stany magazynowe    (pomiedzy zakupem i sprzedaza);   m[0] = m0
var s{0..k} >= 0;   # wielkosci sprzedazy na koncu    miesiaca 1..k
var g{0..k} >= 0;   # gotowka             na koncu    miesiaca 1..k,        g[0] = g0
maximize gotowka: g[k];
subject to    # stany poczatkowe 
c1: m[0]=m0;  # 
c2: g[0]=g0;  # 
c3: s[0]=s0;  #
kup{i in 1..k}: g[i-1] >= z[i] * cena_k[i];                        # kupno
zap{i in 1..k}: m[i-1] - s[i-1] + z[i] <= pojemnosc;               # zapas   ( <= poj.mag.)
pop{i in 1..k}: s[i] <= popyt[i];                                  # popyt
spz{i in 1..k}: s[i] <= m[i];                                      # sprzedaz 
got{i in 1..k}: g[i] = g[i-1] - z[i]*cena_k[i] + s[i]*cena_s[i];   # gotowka
trn{i in 1..k}: m[i-1] - s[i-1] + z[i] = m[i];                     # transakcja
data;
param  k :=    4;
param g0 := 2000;
param m0 :=  100;
param s0 :=    0;
param cena_k := 
  1  200
  2  100
  3  200
  4  300;
param cena_s := 
  1  320
  2  300
  3  250
  4  400;
param popyt := 	
  1   50
  2   75
  3  100
  4   80;
param pojemnosc := 200;
solve;
display z,m,s,g;
end;
#############################################################################################
# :    z     m     s      g      :=
# 0    .    100     0    2000
# 1     0   100    50   18000
# 2   150   200    75   25500
# 3    55   180   100   39500
# 4     0    80    80   71500
# ;
#############################################################################################
